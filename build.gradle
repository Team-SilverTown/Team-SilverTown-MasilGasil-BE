plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'checkstyle'
    id "io.sentry.jvm.gradle" version "4.3.1"
}

group = 'team.silver-town'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // spring starter
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.projectlombok:lombok'

    // guava
    implementation 'com.google.guava:guava:33.0.0-jre'

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // h2
    testImplementation 'com.h2database:h2:2.2.224'

    // spatial
    implementation 'org.hibernate.orm:hibernate-spatial'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // faker
    implementation 'net.datafaker:datafaker:2.1.0'

    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // query dsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // RestTemplate(retry)
    implementation 'org.springframework.retry:spring-retry:1.2.5.RELEASE'

    // aws
    implementation 'io.awspring.cloud:spring-cloud-aws-starter:3.0.0'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.0.0'

    // sentry
    implementation 'io.sentry:sentry-jdbc:7.6.0'

    // snap admin
    implementation 'tech.ailef:snap-admin:0.2.0'

    // test container
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:localstack"
}

def querydslSrcDir = 'src/main/generated'

compileJava {
    options.generatedSourceOutputDirectory = file(querydslSrcDir)
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = true
        html.required = true
    }
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }

    finalizedBy jacocoTestCoverageVerification
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }

            includes = [
                    '*.domain.*',
                    '*.service.*'
            ]

            excludes = [
                    "*.Q*"
            ]
        }
    }

    dependsOn jacocoTestReport
}

clean {
    delete file(querydslSrcDir)
}

